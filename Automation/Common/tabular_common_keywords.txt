*** Settings ***
Library  SeleniumLibrary

*** Keywords ***
Open Browser And Redirect To Url
#    ${DownloadDirectory}  Evaluate    'downloads_{dt.day}_{dt.month}_{dt.year}_{dt.hour}_{dt.minute}_{dt.second}.csv'.format(dt=datetime.datetime.now())    modules=datetime
    ${DOWNLOADDIRECTORY} =  Normalize path  ${CURDIR}/../Downloads
    Set Global Variable  ${DOWNLOADDIRECTORY}
    ${BrowserProfile} =  Create Profile  ${DOWNLOADDIRECTORY}  ${BROWSER}
    Open Browser  url=about:blank  browser=${BROWSER}  options=${BrowserProfile}
    Maximize Browser Window
    Go To  ${URL}
    Wait Until Page Contains Element  ${TabulatorImageLink}

Close Browser Window
    Sleep  3
    Close All Browsers

Log Browser State
    Capture Page Screenshot
    ${Source} =  Get Source
    Log  ${Source}

Get Table Data By Column Number And Row Number  [Arguments]  ${Row}  ${Column}
    ${TableCell} =  Format String  ${TableData}  ${Row}  ${Column}
    ${Data} =    Get Text  ${TableCell}
    [Return]   ${Data}

Get Table Data From Table Pages  [Arguments]  ${ColumnNumber}
    ${TablePages} =  Get Element Count  ${Pages}
    @{AllTableData} =  Create List

    FOR  ${Page}  IN RANGE  1  ${TablePages} + 1
        Click Button    ${Pages}\[${Page}\]
        Read Data From Table  ${ColumnNumber}  ${AllTableData}
    END
    [Return]  ${AllTableData}

Read Data From Table  [Arguments]  ${ColumnNumber}  ${AllTableData}  ${AlphanumericValue}=false
    ${Rows} =  Get Element Count   ${TableVisibleRows}
    FOR   ${Row}  IN RANGE   1  ${Rows} + 1
        ${Data} =  Run Keyword IF  ${ColumnNumber}==2 and '${AlphanumericValue}'=='false'  Get Value Of Task Progress Column In Table  ${Row}
        ...  ELSE IF  ${ColumnNumber}==4 and '${AlphanumericValue}'=='false'  Get Value Of Rating Bar Column In Table  ${Row}
        ...  ELSE IF  ${ColumnNumber}==7 and '${AlphanumericValue}'=='false'  Get Value Of Driver Column In Table  ${Row}
        ...  ELSE  Get Table Data By Column Number And Row Number  ${Row}  ${ColumnNumber}
        Append To List    ${AllTableData}  ${Data}
    END

Get Value Of Task Progress Column In Table    [Arguments]    ${Row}
    ${TableCell} =  Format String    ${TaskProgressColumnData}    ${Row}
    Wait Until Page Contains Element  ${TableCell}
    ${AttributeValue} =  Get Element Attribute  ${TableCell}  style
    @{Data} =  Split String    ${AttributeValue}    ;
    @{Data} =  Split String    ${Data[2]}    :
    @{Data} =  Split String    ${Data[1]}    %
    [Return]    ${${Data[0]}}

Get Value Of Rating Bar Column In Table  [Arguments]    ${Row}
    ${TableCell} =  Format String    ${FilledRatingBar}    ${Row}
    ${FilledRatingBarCount} =    Get Element Count  ${TableCell}
    [Return]  ${${FilledRatingBarCount}}

Get Value Of Driver Column In Table  [Arguments]    ${Row}
    ${TableCell} =  Format String    ${DriverColumn}    ${Row}
    Wait Until Page Contains Element  ${TableCell}
    ${DriverValue} =  Get Element Attribute    ${TableCell}  fill
    ${DriverValue} =  Split String   ${DriverValue}  \#
    ${Data} =  Run Keyword IF  '${DriverValue[1]}'=='2DC214'   Set variable  ${1}  ELSE   Set variable  ${0}
    [Return]  ${Data}
